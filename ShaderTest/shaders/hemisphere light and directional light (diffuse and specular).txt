/* vertex shader */
uniform vec3 ecHemishpereLightPosition; //light position in the view coordinate space.
uniform vec3 SkyColor;
uniform vec3 GroundColor;

uniform mat4 MVMatrix;
uniform mat4 MVPMatrix;
uniform mat3 NormalMatrix;

uniform vec3 ecDirectionalLightPosition;
uniform float SpecularContribution;
uniform float DiffuseContribution;

uniform float HemisphereLightContribution;
uniform float DirectionalLightContribution;

attribute vec4 MCVertex;
attribute vec3 MCNormal;

varying vec3 lightColor;

const vec3 DirectionalLightColor = vec3(1.0, 1.0, 1.0);

vec3 computeHemisphereLightingColor(const in vec3 tnorm, const in vec3 ecPosition){
    vec3 lightDir = normalize(ecHemishpereLightPosition - ecPosition);
    float costheta = dot(tnorm, lightDir);
    float a = costheta * 0.5 + 0.5;
    return mix(GroundColor, SkyColor, a);
}

vec3 computeDirectionalLight(const in vec3 tnorm, const in vec3 ecPosition){
    vec3 lightDir = normalize(ecDirectionalLightPosition - ecPosition);
    float diffuse = max(dot(lightDir, tnorm), 0.0);
    float specular = 0.0;

    if (diffuse > 0.0) {
        vec3 viewDir = normalize(-ecPosition);
        vec3 reflectDir = reflect(-lightDir, tnorm);
        specular = max(dot(reflectDir, viewDir), 0.0);
        specular = pow(specular, 16.0);
    }

    return DirectionalLightColor * (DiffuseContribution * diffuse +  SpecularContribution * specular);
}

void main() {
    vec3 ecPosition = vec3(MVMatrix * MCVertex);
    vec3 tnorm = normalize(NormalMatrix * MCNormal);

    vec3 hemishpereLightingColor = computeHemisphereLightingColor(tnorm, ecPosition);
    vec3 directionalLightingColor = computeDirectionalLight(tnorm, ecPosition);

    lightColor = min((hemishpereLightingColor * HemisphereLightContribution + directionalLightingColor * DirectionalLightContribution), 1.0);
    gl_Position = MVPMatrix * MCVertex;
}

/* fragment shader */

precision mediump float;

vec3 materialColor = vec3(1.0, 1.0, 1.0);

varying vec3 lightColor;

void main(){
    gl_FragColor = vec4(materialColor * lightColor, 1.0);
}

/* play function */
function play(){
    //get the attribute/uniform indices in the shaders.
    var uMVPMatrixIdx = gl.getUniformLocation(program, "MVPMatrix");
    var uNormalMatrixIdx = gl.getUniformLocation(program, "NormalMatrix");
    var uMVMatrixIdx = gl.getUniformLocation(program, "MVMatrix");

    //feed all kinds of matrices
    var modelMatrix = mat4.create();
    var viewMatrix = mat4.create();
    var projectionMatrix = mat4.create();

    var VPMatrix = mat4.create(); //intermediate result used to calculate MVP
    var MVPMatrix = mat4.create();
    var MVMatrix = mat4.create();
    var normalMatrix = mat3.create();

    //projection matrix, view matrix, and VP matrix.
    mat4.perspective(projectionMatrix, Math.PI * 0.1, gl.viewportWidth / gl.viewportHeight, 1, 2000.0);
    mat4.lookAt(viewMatrix, vec3.fromValues(5, 20, 40), vec3.fromValues(0,0,0), vec3.fromValues(0,1,0));
    mat4.multiply(VPMatrix, projectionMatrix, viewMatrix);

    //hemisphere light
    var skyColorIdx = gl.getUniformLocation(program, "SkyColor");
    var groundColorIdx = gl.getUniformLocation(program, "GroundColor");
    gl.uniform3fv(skyColorIdx, vec3.fromValues(1, 1, 1));
    gl.uniform3fv(groundColorIdx, vec3.fromValues(0.1, 0.1, 0.1));

    var ecHemishpereLightPositionIdx = gl.getUniformLocation(program, "ecHemishpereLightPosition");
    var wcHemishpereLightPosition = vec3.fromValues(0, 10, 0);
    var ecHemishpereLightPosition = vec3.create();
    vec3.transformMat4(ecHemishpereLightPosition, wcHemishpereLightPosition, viewMatrix);
    gl.uniform3fv(ecHemishpereLightPositionIdx, ecHemishpereLightPosition);

    //directional light
    var specularContributionIdx = gl.getUniformLocation(program, "SpecularContribution");
    var diffuseContributionIdx = gl.getUniformLocation(program, "DiffuseContribution");
    gl.uniform1f(specularContributionIdx, 0.5);
    gl.uniform1f(diffuseContributionIdx, 0.4);
    var ecDirectionalLightIdx = gl.getUniformLocation(program, "ecDirectionalLightPosition");
    var wcDirectionalLightPosition = vec3.fromValues(-5, 4, 5);
    var ecDirectionalLightPosition = vec3.create();
    vec3.transformMat4(ecDirectionalLightPosition, wcDirectionalLightPosition, viewMatrix);
    gl.uniform3fv(ecDirectionalLightIdx, ecDirectionalLightPosition);

    //light mix
    var hemisphereLightContributionIdx = gl.getUniformLocation(program, "HemisphereLightContribution");
    var directionalLightContributionIdx = gl.getUniformLocation(program, "DirectionalLightContribution");
    gl.uniform1f(hemisphereLightContributionIdx, 0.8);
    gl.uniform1f(directionalLightContributionIdx, 0.3);

    gl.clear(gl.COLOR_BUFFER_BIT | gl.DEPTH_BUFFER_BIT);

    //draw those 9 cuboids
    drawSingleCuboid(0, 0); //正中心的
    drawSingleCuboid(4, 0);
    drawSingleCuboid(-4, 0);

    drawSingleCuboid(0, 4);
    drawSingleCuboid(4, 4);
    drawSingleCuboid(-4, 4);

    drawSingleCuboid(0, -4);
    drawSingleCuboid(4, -4);
    drawSingleCuboid(-4, -4);


    function drawSingleCuboid(x, z){
        mat4.identity(modelMatrix);
        mat4.translate(modelMatrix, modelMatrix, vec3.fromValues(x, 0, z));
        //model matrix and MVP matrix...
        mat4.multiply(MVPMatrix, VPMatrix, modelMatrix);
        gl.uniformMatrix4fv(uMVPMatrixIdx, false, MVPMatrix);

        //normal matrix and MV matrix.
        mat4.multiply(MVMatrix, viewMatrix, modelMatrix);
        mat3.normalFromMat4(normalMatrix, MVMatrix);
        gl.uniformMatrix4fv(uMVMatrixIdx, false, MVMatrix);
        gl.uniformMatrix3fv(uNormalMatrixIdx, false, normalMatrix);

        gl.bindBuffer(gl.ELEMENT_ARRAY_BUFFER, elementBuffer);
        gl.drawElements(gl.TRIANGLES, indices.length, gl.UNSIGNED_BYTE, 0);
    }
}